<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" invoke="onInvoke(event)" visible="false">
	<fx:Script><![CDATA[
		import com.codeazur.as3swf.SWF;
		import com.codeazur.as3swf.exporters.SVGShapeExporter;
		import com.codeazur.as3swf.tags.*;
		
		private function onInvoke(e: InvokeEvent): void {
			var bytes: ByteArray = new ByteArray();
			
			var swfFile: FileStream = new FileStream();
			swfFile.open(e.currentDirectory.resolvePath(e.arguments[0]), FileMode.READ);
			swfFile.readBytes(bytes);
			swfFile.close();
			
			var swf: SWF = new SWF(bytes);
			var svgExporter: SVGShapeExporter = new SVGShapeExporter(swf);
			for each (var tag: ITag in swf.tags) {
				trace(tag);
				if (tag is TagDefineShape)
					TagDefineShape(tag).export(svgExporter);
			}
			
			var inName: String = e.arguments[0];
			var outName: String = inName.substr(0, inName.length - 3) + "svg";
			
			var svgFile: FileStream = new FileStream();
			svgFile.open(e.currentDirectory.resolvePath(outName), FileMode.WRITE);
			svgFile.writeUTFBytes(svgExporter.svg);
			trace(e.currentDirectory.resolvePath(outName).nativePath);
			svgFile.close();
			
			var exitingEvent:Event = new Event(Event.EXITING, false, true);
			NativeApplication.nativeApplication.dispatchEvent(exitingEvent);
			if (!exitingEvent.isDefaultPrevented())
				NativeApplication.nativeApplication.exit();
		}
	]]></fx:Script>
</s:WindowedApplication>
